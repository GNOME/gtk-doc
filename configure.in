dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.52)

dnl Use a simple 2-digit version number for a while, since our old example
dnl Makefile can only cope with that, i.e. use 1.1, 1.2, 1.3 ... 9.9.
m4_define(gtk_doc_version, 1.1)

AC_INIT([gtk-doc], [gtk_doc_version],
        [http://bugzilla.gnome.org/enter_bug.cgi?product=gtk-doc])

AC_CONFIG_SRCDIR([gtk-doc.dsl.in])

AM_INIT_AUTOMAKE(AC_PACKAGE_TARNAME, AC_PACKAGE_VERSION)

AC_ISC_POSIX
AC_PROG_CC

dnl
dnl Check for Perl.
dnl
AC_PATH_PROG([PERL], [perl])
if test -z "$PERL"; then
  AC_MSG_ERROR([perl not found])
fi

AC_MSG_CHECKING([if Perl version >= 5.6.0])
if "$PERL" -e "require v5.6.0"; then
  AC_MSG_RESULT([yes])
else
  AC_MSG_RESULT([no])
  AC_MSG_ERROR([perl >= 5.6.0 is required for gtk-doc])
fi

dnl
dnl Check for jade or openjade.
dnl
dnl We prefer to use openjade with the '-t sgml-raw' option, since '-t sgml'
dnl adds a newline before the closing '>' of HTML tags. lynx can't parse
dnl that, which is bad for accessibility.
dnl
SGML_FORMAT_TYPE=sgml-raw
AC_PATH_PROG([JADE], [openjade])
if test -z "$JADE"; then
  SGML_FORMAT_TYPE=sgml
  AC_PATH_PROG([JADE], [jade])
  if test -z "$JADE"; then
    AC_MSG_ERROR([openjade/jade not found])
  fi
fi
AC_SUBST([SGML_FORMAT_TYPE])

dnl
dnl Check for xsltproc
dnl
AC_PATH_PROG([XSLTPROC], [xsltproc])
if test -z "$XSLTPROC"; then
  AC_MSG_ERROR([xsltproc not found])
fi

dnl check for DocBook DTD and stylesheets in the local catalog.
JH_CHECK_XML_CATALOG([-//OASIS//DTD DocBook XML V4.1.2//EN],
   [DocBook XML DTD V4.1.2])
JH_CHECK_XML_CATALOG([http://docbook.sourceforge.net/release/xsl/current/html/chunk.xsl],
   [DocBook XSL Stylesheets])

dnl
dnl Set PACKAGE_DATA_DIR so we can find the script containing common routines.
dnl
if test "x${datadir}" = 'x${prefix}/share'; then
  if test "x${prefix}" = "xNONE"; then
    PACKAGE_DATA_DIR="${ac_default_prefix}/share/${PACKAGE}/data"
  else
    PACKAGE_DATA_DIR="${prefix}/share/${PACKAGE}/data"
  fi
else
  PACKAGE_DATA_DIR="${datadir}/${PACKAGE}/data"
fi

AC_SUBST([PACKAGE_DATA_DIR])


dnl Only use -Wall if we have gcc
if test "x$GCC" = "xyes"; then
  if test -z "`echo "$CFLAGS" | grep "\-Wall" 2> /dev/null`" ; then
    CFLAGS="$CFLAGS -Wall"
  fi
fi


AC_CONFIG_FILES([Makefile 
gtk-doc.pc
gtk-doc.dsl 
gtk-doc.spec
gtkdoc-common.pl
])

dnl run chmod on these after parsing them.
AC_CONFIG_FILES([gtkdoc-mkman],    [chmod +x gtkdoc-mkman])
AC_CONFIG_FILES([gtkdoc-mkhtml],   [chmod +x gtkdoc-mkhtml])
AC_CONFIG_FILES([gtkdoc-fixxref],  [chmod +x gtkdoc-fixxref])
AC_CONFIG_FILES([gtkdoc-mkdb],     [chmod +x gtkdoc-mkdb])
AC_CONFIG_FILES([gtkdoc-mktmpl],   [chmod +x gtkdoc-mktmpl])
AC_CONFIG_FILES([gtkdoc-scan],     [chmod +x gtkdoc-scan])
AC_CONFIG_FILES([gtkdoc-scanobj],  [chmod +x gtkdoc-scanobj])
AC_CONFIG_FILES([gtkdoc-scangobj], [chmod +x gtkdoc-scangobj])
AC_CONFIG_FILES([gtkdocize],       [chmod +x gtkdocize])

AC_OUTPUT
